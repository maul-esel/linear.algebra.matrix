system linear.algebra.matrix.semantics.Typing
	extends linear.algebra.matrix.semantics.Casting

import linear.algebra.matrix.matrix.*

auxiliary {
	genericVarMatches(GenericRegister genReg, String variable, String value) : Boolean
	checkGenericMatrixType(GenericRegister genReg, MatrixType paramType, MatrixType valueType, Expression param)
	genericTypeMatches(GenericRegister genReg, GenericType generic, MathematicalType value) : Boolean
	genericTypeArgumentMatches(GenericRegister genReg, GenericType generic, MathematicalType value) : Boolean
	isGeneric(String size) : Boolean
	multipliedMatrixType(MatrixType left, MatrixType right) : MatrixType
	transpose(MatrixType type) : MatrixType
	scalarMultType(MathematicalType scalar, MatrixType matr) : MatrixType
	createMatrixType(String height, String width, MathematicalType entryType) : MatrixType
	makeExactFromGeneric(GenericRegister generics, MatrixType matrType) : MatrixType
}

judgments {
	type |- Expression expr : output Type
		error "Cannot type " + stringRep(expr)
		source expr
    vartype ||- VarDeclaration decl : output Type
    invocation |= CallableDeclaration decl <~! CallableInvocation<?> call |> output GenericRegister
}

auxiliary multipliedMatrixType(MatrixType left, MatrixType right) {
	empty |- left.realEntryType ~~ right.realEntryType |> var MathematicalType superType
	createMatrixType(left.height, right.width, superType)
}

auxiliary transpose(MatrixType type) {
	createMatrixType(type.width, type.height, type.realEntryType)
}

auxiliary isGeneric(String variable) {
	variable.startsWith('$')
}

auxiliary checkGenericMatrixType(GenericRegister genReg, MatrixType paramType, MatrixType valueType, Expression param) {
	(genericVarMatches(genReg, paramType.height, valueType.height)
		&& genericVarMatches(genReg, paramType.width, valueType.width))
	 or fail error "generic param matrix type dimensions do not match"
		source param

	if (paramType.realEntryType instanceof GenericType) {
		genericTypeMatches(genReg, paramType.realEntryType as GenericType, valueType.realEntryType)
		or fail error "generic param matrix entry type does not match"
			source param
	}
}

auxiliary genericVarMatches(GenericRegister genReg, String variable, String value) {
	if (isGeneric(variable)) {
		if (!genReg.existsNumber(variable)) {
			genReg.putNumber(variable, value)
			true
		} else
			(genReg.getNumber(variable) == value)
	} else true
}

auxiliary genericTypeMatches(GenericRegister genReg, GenericType generic, MathematicalType value) {
	if (!genReg.existsType(generic)) {
		genReg.putType(generic, value)
		true
	} else {
		try {
			empty |- genReg.getType(generic) ~~ value |> var MathematicalType common
			genReg.putType(generic, common)
			true
		} catch (Exception e) { false }
	}
}

auxiliary genericTypeArgumentMatches(GenericRegister genReg, GenericType generic, MathematicalType value) {
	if (!genReg.existsType(generic)) {
		genReg.putType(generic, value)
		true
	} else {
		try {
			empty |- genReg.getType(generic) <: value
			genReg.putType(generic, value)
			true
		} catch (Exception e) { false }
	}
}

auxiliary scalarMultType(MathematicalType scalar, MatrixType matr) {
	var MatrixType result
	{
		empty |- scalar <: matr.realEntryType
		result = matr
	} or {
		empty |- matr.realEntryType <: scalar
		result = createMatrixType(matr.height, matr.width, scalar)
	}
	result
}

auxiliary createMatrixType(String height, String width, MathematicalType entryType) {
	val newType = MatrixFactory.eINSTANCE.createMatrixType()
	newType.height = height
	newType.width  = width
	newType.entryType = entryType.copy
	newType
}

auxiliary makeExactFromGeneric(GenericRegister generics, MatrixType matrType) {
	createMatrixType(
		if (isGeneric(matrType.height)) generics.getNumber(matrType.height) else matrType.height,
		if (isGeneric(matrType.width))  generics.getNumber(matrType.width)  else matrType.width,
		if (matrType.realEntryType instanceof GenericType)
			generics.getType(matrType.realEntryType as GenericType)
		else matrType.realEntryType
	)
}

/* INVOCATION */
rule IVCallable
	G |= CallableDeclaration decl <~! CallableInvocation<?> call |> GenericRegister reg
from {
	{ decl.params.params.size == call.params.size }

	reg = new GenericRegister()

	// check parameters and infer generic types
	val paramIterator = decl.params.params.iterator()
	for (value : call.params) {
		G ||- paramIterator.next : var Type paramType
		G |- value : var Type valueType

		if (valueType instanceof MatrixType && paramType instanceof MatrixType && (paramType as MatrixType).isGeneric())
			checkGenericMatrixType(reg, paramType as MatrixType, valueType as MatrixType, value)
		else if (paramType instanceof GenericType && valueType instanceof MathematicalType) {
			genericTypeMatches(reg, paramType as GenericType, valueType as MathematicalType)
			or fail error "generic param type does not match"
				source value
		} else {
			empty |- valueType <: paramType
			or fail error "type " + stringRep(valueType) + " does not match param of type " + stringRep(paramType)
				source value
		}
	}

	// add generic type arguments
	val typeIterator = call.ref.genericArguments.types.iterator()
	for (t : call.ref.ref.genericTypes) {
		if (typeIterator.hasNext()) {
			{ genericTypeArgumentMatches(reg, t, typeIterator.next()) }
			or fail error "Invalid type argument for '" + t.name + "'"
				source call
		} else if (!reg.existsType(t)) { // no more specified, not inferred by arguments
			fail error "Not all generic type arguments are specified or inferred: missing '" + t.name + "'"
				source call
		}
	}
	{ !typeIterator.hasNext() }
	or fail error "Too many type arguments"
		source call

	// add generic number arguments
	val numberIterator = call.ref.genericArguments.numbers.iterator()
	for (v : call.ref.ref.genericVariables) {
		if (numberIterator.hasNext()) {
			{ genericVarMatches(reg, v.name, numberIterator.next()) }
			or fail error "Invalid number argument for '" + v.name + "'"
				source call
		} else if (!reg.existsNumber(v.name)) {
			fail error "Not all generic number arguments are specified or inferred: missing '" + v.name + "'"
				source call
		}
	}
	{ !numberIterator.hasNext() }
	or fail error "Too many number arguments"
		source call
}

/* TYPING */
axiom TTypedVarDecl
	G ||- TypedVarDeclaration variable : variable.type

rule TInferredVarDecl
	G ||- InferredVarDeclaration decl : Type type
from {
	G |- decl.value : type
}

rule TTupleVarDeclaration
	G ||- TupleVarDeclaration decl : Type type
from {
	type = decl.type ?: {
		val container = decl.eContainer as CombinedTupleVarDeclaration
		G |- container.tuple : var TupleType tupleType
		tupleType.types.get(decl.componentIndex)
	}
}

axiom TIntegerLiteral
    G |- IntLiteral expr : MatrixFactory.eINSTANCE.createIntegerType()

axiom TBooleanLiteral
	G |- BooleanLiteral bool : MatrixFactory.eINSTANCE.createBooleanType()

axiom TGenericVariable
	G |- GenericVariable gen : MatrixFactory.eINSTANCE.createIntegerType()

axiom TRealLiteral
	G |- RealLiteral real : MatrixFactory.eINSTANCE.createRealType()

axiom TImaginaryLiteral
	G |- ImaginaryLiteral im : MatrixFactory.eINSTANCE.createComplexType()

axiom TTypeCheckExpression
	G |- TypeCheckExpression check : MatrixFactory.eINSTANCE.createBooleanType()

axiom TTypeComparisonExpression
	G |- TypeComparisonExpression comp : MatrixFactory.eINSTANCE.createBooleanType()

axiom TCastExpression
	G |- CastExpression cast : cast.type

rule TVariable
	G |- Variable variable : Type type
from {
	G ||- variable.ref : type
}

rule TTuple
	G |- TupleExpression tuple : TupleType type
from {
	type = MatrixFactory.eINSTANCE.createTupleType()
	for (entry : tuple.entries) {
		G |- entry : var Type entryType
		type.types.add(entryType)
	}
}

rule TFunctionCall
	G |- FunctionCall call : Type type
from {
	G |= call.func.ref <~! call |> var GenericRegister genericRegister

	val retType = call.func.ref.returnType
	type = if (retType instanceof MatrixType) {
		if (retType.isGeneric())
			makeExactFromGeneric(genericRegister, retType)
		else
			retType
	} else if (retType instanceof GenericType)
		genericRegister.getType(retType)
	else
		retType
}

rule TDeterminantOrAbsoluteValue
	G |- DeterminantOrAbsoluteValue expr : MathematicalType type
from {
	G |- expr.expr : var Type innerType

	if (innerType instanceof MatrixType) {
		{ innerType.isSquare }
		or fail error "Matrix determinant is only defined for square matrices" source expr
		type = innerType.entryType
	} else if (innerType instanceof ComplexType)
		type = MatrixFactory.eINSTANCE.createRealType()
	else if (innerType instanceof MathematicalType)
		type = innerType
	else {
		fail error "Cannot type determinant or absolute value of type " + stringRep(innerType) source expr
	}
}

rule TMultiLineMatrixLiteral
	G |- MultiLineMatrixLiteral lit : MatrixType type
from {
	(lit.hasConsistentLineWidth)
	or fail error "matrix lines must all have the same width" source lit

	val lines = lit.lines.size()
	val columns = lit.lines.get(0).getEntries().size()

	val entryType = lit.entryType ?: { // if no entryType specified, derive it from entries
		val expressionEntries = (lit.lines.map [ entries ].flatten.filter(Expression) + #[lit.dot?.fill].filterNull).toSet
		G |- expressionEntries.head : var MathematicalType seed
		expressionEntries.fold(seed) [ common, currentExpr |
			var MathematicalType result
			{
				G |- currentExpr : var MathematicalType currentType
				G |- common ~~ currentType |> result
			} or fail error "cannot compute common super type of " + stringRep(common) + " with " + stringRep(currentExpr) source currentExpr
			result
		]
	}
	type = createMatrixType(Integer.toString(lines), Integer.toString(columns), entryType)
}

rule TMatrixInit
	G |- MatrixInit init : MatrixType type
from {
	var MathematicalType entryType = init.entryType
	if (entryType == null)
		G |- init.entry : entryType
	type = createMatrixType(init.lineMax, init.colMax, entryType)
}

rule TParenthesis
	G |- ParenthesizedExpression expr : Type type
from {
	G |- expr.expr : type
}

rule TVariableAssignment
	G |- VariableAssignment assign : Type vartype
from {
	G |- assign.variable : vartype
	G |- assign.value : var Type valtype
	G |- valtype <: vartype or fail error "value cannot be assigned" source assign.value
}

rule TMatrixEntryAssignment
	G |- MatrixEntryAssignment assign : MathematicalType type
from {
	G |- assign.matr : var MatrixType matrType
	G |- assign.value : var Type valType
	G |- valType <: matrType.realEntryType or fail error "entry cannot be assigned" source assign.value
	type = matrType.realEntryType
}

rule TTupleAssignment
	G |- TupleAssignment assign : TupleType type
from {
	// type compatibility checked in Checks, not here
	G |- assign.tuple : type
}

rule TLogical
	G |- LogicalAndOr log : BooleanType bool
from {
	G |- log.left : bool
	G |- log.right : bool
}

rule TEquals
	G |- Equals eq : MatrixFactory.eINSTANCE.createBooleanType()
from {
	G |- eq.left : var Type leftType
	G |- eq.right : var Type rightType

	{ G |- leftType <: rightType } or { G |- rightType <: leftType }
}

rule TComparison
	G |- Comparison comp : MatrixFactory.eINSTANCE.createBooleanType()
from {
	G |- comp.left : var MathematicalType leftType
	G |- comp.right : var MathematicalType rightType
	!(leftType instanceof ComplexType) && !(rightType instanceof ComplexType) // complex numbers are not ordered
}

rule TEquivalency
	G |- Equivalency eq : MatrixFactory.eINSTANCE.createBooleanType()
from {
	G |- eq.left : var MatrixType leftType
	G |- eq.right : var MatrixType rightType
	G |- leftType <: rightType
}

rule TAddition
	G |- Addition add : Type type
from  {
	G |- add.left  : var Type leftType
	G |- add.right : var Type rightType

	!(leftType instanceof BooleanType) && !(rightType instanceof BooleanType)

	{
		G |- leftType <: rightType
		type = rightType
	}
	or {
		G |- rightType <: leftType
		type = leftType
	} or fail error "cannot type addition of type " + stringRep(leftType) + " and " + stringRep(rightType)
		source add
}

rule TDivision
	G |- Division div : MathematicalType type
from {
	G |- div.left : var MathematicalType leftType
	G |- div.right : var MathematicalType rightType
	if (leftType instanceof ComplexType || rightType instanceof ComplexType)
		type = MatrixFactory.eINSTANCE.createComplexType()
	else if (leftType instanceof RealType || rightType instanceof RealType)
		type = MatrixFactory.eINSTANCE.createRealType()
	else
		type = MatrixFactory.eINSTANCE.createRationalType()
}

rule TMultiplication
	G |- Multiplication mult : Type type
from {
	G |- mult.left  : var Type leftType
	G |- mult.right : var Type rightType

	if (leftType instanceof MatrixType && rightType instanceof MatrixType) { // matrix multiplication
		((leftType as MatrixType).canMultiply(rightType as MatrixType))
		or fail error "Matrix multiplication is not defined"
			source mult
		type = multipliedMatrixType(leftType as MatrixType, rightType as MatrixType)
	}
	else if (leftType instanceof MatrixType) {
		(rightType instanceof MathematicalType) // right scalar multiplication
		or fail error "Scalar multiplication not defined"
			source mult
		type = scalarMultType(rightType as MathematicalType, leftType)
	}
	else if (rightType instanceof MatrixType) {
		(leftType instanceof MathematicalType) // left scalar multiplication
		or fail error "Scalar multiplication not defined"
			source mult
		type = scalarMultType(leftType as MathematicalType, rightType)
	}
	else {
		(leftType instanceof MathematicalType && rightType instanceof MathematicalType)
		or fail error "Multiplication of " + stringRep(leftType) + " and " + stringRep(rightType) + " undefined"
			source mult
		G |- leftType ~~ rightType |> type
	}
}

rule TUnaryMinus
	G |- UnaryMinus un : Type type
from {
	G |- un.expr : type
}

rule TTransposition
	G |- Transposition t : MatrixType type
from {
	G |- t.expr : var MatrixType baseType
	type = transpose(baseType)
}

rule TExponentiation
	G |- Exponentiation exp : Type baseType
from {
	G |- exp.base : baseType

	{ (baseType instanceof MathematicalType) }
	or { (baseType instanceof MatrixType && (baseType as MatrixType).isSquare) }

	G |- exp.exp : var Type expType
	(expType instanceof IntegerType)
}

rule TNegation
	G |- Negation neg : BooleanType bool
from {
	G |- neg.expr : bool
}

rule TTernary
	G |- Ternary ter : Type type
from {
	G |- ter.cond : var BooleanType bool
	G |- ter.ifTrue : var Type trueType
	G |- ter.ifFalse : var Type falseType

	{
		G |- trueType <: falseType
		type = falseType
	} or {
		G |- falseType <: trueType
		type = trueType
	} or fail error "expression doesn't have matching type" source ter.ifFalse
}

rule TEntryAccess
	G |- EntryAccess acc : MathematicalType type
from {
	var IntegerType intType
	G |- acc.matr : var MatrixType matrType
	G |- acc.line : intType
	G |- acc.column : intType
	type = matrType.realEntryType
}