system linear.algebra.matrix.semantics.Checks extends linear.algebra.matrix.semantics.Typing

import org.eclipse.emf.ecore.EObject
import linear.algebra.matrix.matrix.*
import linear.algebra.matrix.interpreter.VariableRegister

auxiliary {
	hasPlaceholders(MultiLineMatrixLiteral lit) : boolean
	isGenericInParamList(SmartMatrixType type, ParameterList list) : boolean
	isGenericVariableDefined(String generic, ParameterList list) : boolean
	alwaysReturns(EObject obj) : boolean
}

judgments {
	G |- Object just_a |> Object dummy // XSemantics won't accept an empty judgments section
}

auxiliary hasPlaceholders(MultiLineMatrixLiteral lit) {
	var placeholders = false;
	for (line : lit.lines) {
		for (entry : line.entries) {
			placeholders = placeholders || (entry instanceof PlaceholderEntry)
		}
	}
	placeholders
}

auxiliary isGenericInParamList(SmartMatrixType type, ParameterList list) {
	val height = type.height
	val width = type.width

	var heightFound = !isGeneric(height) // not generic dimensions are assumed to be "found" i.e. valid
	var widthFound = !isGeneric(width)

	for (param : list.params) {
		empty ||- param : var Type paramType
		if (paramType instanceof MatrixType) {
			heightFound = heightFound || paramType.height == height || paramType.width == height
			widthFound = widthFound || paramType.height == width || paramType.width == width
		}
	}
	widthFound && heightFound
}

auxiliary isGenericVariableDefined(String generic, ParameterList list) {
	var found = false
	for (param : list.params) {
		empty ||- param : var Type paramType
		if (paramType instanceof MatrixType)
			found = found || paramType.height == generic || paramType.width == generic
	}
	found
}

auxiliary alwaysReturns(EObject obj) {
	false
}

auxiliary alwaysReturns(ReturnStatement ret) {
	true
}

auxiliary alwaysReturns(Block block) {
	var returns = false
	for (line : block.lines)
		returns = returns || alwaysReturns(line)
	returns
}

auxiliary alwaysReturns(IfElse ifElse) {
	if (ifElse.getElse == null)
		false
	else
		alwaysReturns(ifElse.ifTrue) && alwaysReturns(ifElse.getElse)
}

auxiliary alwaysReturns(FromToLoop loop) {
	alwaysReturns(loop.body)
}

auxiliary alwaysReturns(WhileLoop loop) {
	alwaysReturns(loop.body)
}

checkrule CVarDeclarationIsTyped
	for VarDeclaration variable
from {
	empty ||- variable : var Type vartype
}

checkrule CAssignmentIsTyped
	for Assignment assign
from {
	empty |- assign : var Type type
}

checkrule CFunctionCallIsTyped
	for FunctionCall call
from {
	empty |- call : var Type type
}

checkrule CReturnStatementIsTyped
	for ReturnStatement ret
from {
	if (ret.value != null) {
		empty |- ret.value : var Type type
	}
}

checkrule CIfConditionIsTyped
	for IfElse ifelse
from {
	empty |- ifelse.cond : var BooleanType type
}

checkrule CMatrixLiteralHasDot
	for MultiLineMatrixLiteral lit
from {
	{ lit.dot != null || !hasPlaceholders(lit) }
	or fail error "Literal references undefined placeholder '.'"
		source lit
}

checkrule CNoConstAssign
	for VariableAssignment assign
from {
	(!assign.variable.ref.const)
	or fail
		error "Cannot assign value to constant variable"
		source assign
}

checkrule CConstVarHasValue
	for VarDeclaration variable
from {
	{ (!variable.const || variable.value != null) }
	or fail error "Constant variable must be assigned a value on declaration"
		source variable
}

checkrule CInferredVarHasValue
	for InferredVarDeclaration variable
from {
	{ variable.value != null }
	or fail error "Variable with inferred type must be assigned value"
		source variable
}

checkrule CVarDeclHasCompatibleValueType
	for VarDeclaration variable
from {
	if (variable.value != null) {
		empty ||- variable : var Type vartype
		empty |- variable.value : var Type valtype
		empty |- vartype <: valtype
		or fail error "Value of type " + stringRep(valtype) + " cannot be assigned to variable of type " + stringRep(vartype)
			source variable.value
	}
}

checkrule CGenericVarDeclarationDefined
	for VarDeclaration variable
from {
	empty ||- variable : var Type vartype

	var container = variable.eContainer;
	while (container != null && !(container instanceof FuncDeclaration) && !(container instanceof ProcDeclaration))
		container = container.eContainer;

	if ((vartype instanceof MatrixType) && (vartype as SmartMatrixType).isGeneric()) {
		(container != null) && 	(
			(container instanceof FuncDeclaration && (isGenericInParamList(vartype as SmartMatrixType, (container as FuncDeclaration).params)))
			|| (container instanceof ProcDeclaration && (isGenericInParamList(vartype as SmartMatrixType, (container as ProcDeclaration).params)))
		)
		or fail error "Variable definition references unknown generic types"
			source variable
	}
}

checkrule CGenericVariable
	for GenericVariable gen
from {
	var container = gen.eContainer;
	while (container != null && !(container instanceof FuncDeclaration) && !(container instanceof ProcDeclaration))
		container = container.eContainer;

	(container != null) && (
		(container instanceof FuncDeclaration && (isGenericVariableDefined(gen.name, (container as FuncDeclaration).params)))
		|| (container instanceof ProcDeclaration && (isGenericVariableDefined(gen.name, (container as ProcDeclaration).params)))
	)
	or fail error "References undefined generic variable"
		source gen
}

checkrule CVarDeclarationIsUnique
	for VarDeclaration decl
from {
	val container = decl.eContainer
	for (other : container.eContents)
		{ !(other instanceof VarDeclaration) || ((other as VarDeclaration).name != decl.name) || (other == decl) }
		or fail error "variable names must be unique"
			source decl
}

checkrule CFunctionCallHasValidParameters
	for FunctionCall call
from {
	{ call.func.ref.params.params.size == call.params.size }
	or fail error "Function call has invalid number of parameters (expected " + call.func.ref.params.params.size + ", found " + call.params.size + ")"
		source call
}

checkrule CFunctionDeclarationHasNoDefaultValues
	for FuncDeclaration decl
from {
	for (param : decl.params.params) {
		param.value == null
		or fail error "Parameter default values are not allowed"
			source param.value
	}
}

checkrule CFuncDeclarationGenericReturnTypeDefined
	for FuncDeclaration func
from {
	if (func.returnType instanceof MatrixType) {
		val retType = SmartMatrixType.copy(func.returnType as MatrixType);
		if (retType.isGeneric()) {
			(isGenericInParamList(retType, func.params))
			or fail error "Generic return type " + stringRep(retType) + " is not defined"
				source func.returnType
		}
	}
}

checkrule CFuncDeclarationAlwaysReturns
	for FuncDeclaration func
from {
	alwaysReturns(func.body)
	or fail error "Not all code paths return a value"
		source func
}

checkrule CReturnStatement
	for ReturnStatement ret
from {
	var container = ret.eContainer
	while (container != null && !(container instanceof FuncDeclaration) && !(container instanceof ProcDeclaration))
		container = container.eContainer

	{ (container != null) }
	or fail error "Return must be inside a function or proc declaration"
		source ret

	if (container instanceof FuncDeclaration) {
		{ ret.value != null }
		or fail error "Return must specify return value"
			source ret

		{
			empty |- ret.value : var Type type
			empty |- type <: container.returnType
		} or fail error "expression type does not match function return type"
			source ret.value
	}
	else if (container instanceof ProcDeclaration) {
		ret.value == null
		or fail error "Proc cannot return a value"
			source ret.value
	}
}

checkrule CProcCallHasValidParameters
	for ProcCall call
from {
	{ call.proc.ref.params.params.size == call.params.size }
	or fail error "Proc call has invalid number of parameters (expected " + call.proc.ref.params.params.size + ", found " + call.params.size + ")"
		source call

	val genReg = new VariableRegister();
	{ checkParamTypes(genReg, call.proc.ref.params.params, call.params) }
}

checkrule CProcDeclarationHasNoDefaultValues
	for ProcDeclaration proc
from {
	for (param : proc.params.params) {
		param.value == null
		or fail error "Parameter default values are not allowed"
			source param.value
	}
}

checkrule CFromToLoop
	for FromToLoop loop
from {
	{ !loop.getVar.ref.const }
	or fail error "Cannot use constant as loop counter"
		source loop.getVar

	var IntegerType intType
	empty ||- loop.getVar.ref : intType
		or fail error "counter var must be integer" source loop.getVar
	empty |- loop.init : intType
		or fail error "initial value must be integer" source loop.init
	empty |- loop.end : intType
		or fail error "end value must be integer" source loop.end
}

checkrule CWhileLoop
	for WhileLoop loop
from {
	empty |- loop.cond : var BooleanType bool
	or fail error "condition must be boolean"
		source loop.cond
}

checkrule CBlockHasNoUnreachableCode
	for Block block
from {
	var reachable = true;
	for (line : block.lines) {
		{ reachable }
		or fail error "Unreachable code detected"
			source line
		if (alwaysReturns(line))
			reachable = false
	}
}

checkrule CImportIsUnique
	for Import imp
from {
	(imp.eContainer as Code).imports.forEach [ other |
		{ other.getSource != imp.getSource || other == imp }
		or fail error "Duplicate import of '" + imp.getSource + "'"
			source imp
	]
}