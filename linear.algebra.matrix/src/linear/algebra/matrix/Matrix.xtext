grammar linear.algebra.matrix.Matrix
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.algebra.linear/matrix/Matrix"

// I. General code structure
Code hidden(WHITESPACE, NEWLINE, SL_COMMENT, ML_COMMENT) :
	(imports+=Import)*
	(lines+=Line)*
;

Import :
	'import' source=ID ('as' alias=ID)? ';'
;

Line :
	Action | FuncDeclaration | ProcDeclaration
;

Action :
	(TypedVarDeclaration ';')
	| (InferredVarDeclaration ';')
	| (CombinedTupleVarDeclaration ';')
	| (VariableAssignment ';')
	| (MatrixEntryAssignment ';')
	| (TupleAssignment ';')
	| (FunctionCall ';')
	| ReturnStatement
	| FailureStatement
	| IfElse
	| ProcCall
	| FromToLoop
	| WhileLoop // TODO: solve proc/func conflict without backtracking
;

InferredVarDeclaration :
	(const?=CONST | VAR) name=ID OP_ASSIGN value=Expression
;

TypedVarDeclaration :
	(const?=CONST | VAR) name=ID OP_COLON type=Type (OP_ASSIGN value=Expression)?
;

CombinedTupleVarDeclaration :
	'('
		decls+=(TupleVarDeclaration|EmptyTupleVarDeclarationComponent)
		(',' decls+=(TupleVarDeclaration|EmptyTupleVarDeclarationComponent))+
	')'
	OP_ASSIGN
	tuple=Expression
;

TupleVarDeclaration :
	(const?=CONST | VAR) name=ID (OP_COLON type=Type)?
;

EmptyTupleVarDeclarationComponent :
	{EmptyTupleVarDeclarationComponent}
;

ReturnStatement :
	{ReturnStatement} RETURN (value=Expression)? ';'
;

FailureStatement :
	{FailureStatement} FAIL msg=FailureMessage? ';'
;

FailureMessage :
	{FailureMessage} fragments+=(LiteralMessageFragment|ExpressionMessageFragment)*
;

LiteralMessageFragment :
	content=STRING
;

ExpressionMessageFragment :
	'#{' expr=Expression '}'
;

ProcRef hidden() :
	ref=[ProcDeclaration|QualifiedProcName]
	genericArguments=GenericArguments
	'('
;

GenericArguments :
	{GenericArguments}
	('#[' _ (
		(types+=MathematicalType (_ ',' _ types+=MathematicalType)*)
		| ((types+=MathematicalType (_ ',' _ types+=MathematicalType)*) _ ',' _ (numbers+=(POSITIVE_INTEGER|GENERIC_VAR) (_ ',' _ numbers+=(POSITIVE_INTEGER|GENERIC_VAR))*))
		| (numbers+=(POSITIVE_INTEGER|GENERIC_VAR) (_ ',' _ numbers+=(POSITIVE_INTEGER|GENERIC_VAR))*)
	) _ ']')?
;

_ :
	(SL_COMMENT | ML_COMMENT | WHITESPACE | NEWLINE)*
;

QualifiedProcName :
	(ID '::')? PROC_NAME
;

ProcCall :
	ref=ProcRef (params+=Expression (',' params+=Expression)*)? ')' ';'
;

FromToLoop :
	'from' var=Variable '=' init=Expression 'to' end=Expression body=Block
;

WhileLoop :
	'while' cond=Expression body=Block
;

Block: 
	{Block} '{' (lines+=Action)* '}'
;

// I. a) conditionals
IfBody :
	Action | Block
;

IfElse :
	IF '(' cond=Expression ')' ifTrue=IfBody
	(=>ELSE else=IfBody)?
;

// I. b) Function and proc declarations
FuncDeclaration :
	(internal?=INTERNAL)? DEF name=ID
	('#[' (
		(genericTypes+=GenericType (',' genericTypes+=GenericType)*)
		| ((genericTypes+=GenericType (',' genericTypes+=GenericType)*) ','(genericVariables+=GenericVariable (',' genericVariables+=GenericVariable)*))
		| (genericVariables+=GenericVariable (',' genericVariables+=GenericVariable)*)
	) ']')?
	OP_COLON params=ParameterList '->' returnType=Type body=Block
;

ProcDeclaration :
	(internal?=INTERNAL)? PROC name=PROC_NAME
	('#[' (
		(genericTypes+=GenericType (',' genericTypes+=GenericType)*)
		| ((genericTypes+=GenericType (',' genericTypes+=GenericType)*) ','(genericVariables+=GenericVariable (',' genericVariables+=GenericVariable)*))
		| (genericVariables+=GenericVariable (',' genericVariables+=GenericVariable)*)
	) ']')?
	OP_COLON params=ParameterList body=Block
;

ParameterList :
	{ParameterList} '(' (params+=ParamDeclaration (',' params+=ParamDeclaration)*)? ')'
;

ParamDeclaration :
	name=ID OP_COLON type=Type
;

// II. types
GenericType:
	name=GENERIC_TYPE
;

MathematicalType :
	{IntegerType} INT
	| {IntegerType} 'ℤ'
	| {RationalType} 'rational'
	| {RationalType} 'ℚ'
	| {RealType} 'real'
	| {RealType} 'ℝ'
	| {ComplexType} 'complex'
	| {ComplexType} 'ℂ'
	| GenericType
;

TupleType :
	'(' types+=Type (',' types+=Type)+ ')'
;

MatrixType :
	entryType=MathematicalType? '(' height=(POSITIVE_INTEGER|GENERIC_VAR) ('°'|X) width=(POSITIVE_INTEGER|GENERIC_VAR) ')'
;

Type :
	MatrixType | TupleType | MathematicalType | {BooleanType} BOOL
;

// III. Expressions
// III. a) Literals
BooleanLiteral :
	value=('true'|'false')
;

IntLiteral :
	value=(POSITIVE_INTEGER | ZERO | NEGATIVE_INTEGER)
;

RealLiteral :
	value=DOUBLE
;

ImaginaryLiteral hidden() :
	coefficient=(IntLiteral | RealLiteral) IMAGINARY_UNIT
;

MathematicalLiteral :
	ImaginaryLiteral | IntLiteral | RealLiteral
;

// III. a) (i) Matrix literals
MatrixInit :
	'init' entryType=MathematicalType? '(' line=MatrixInitVarDeclaration '..' lineMax=(POSITIVE_INTEGER|GENERIC_VAR) ',' column=MatrixInitVarDeclaration '..' colMax=(POSITIVE_INTEGER|GENERIC_VAR) ')' '~>' entry=Expression
;

MatrixInitVarDeclaration :
	VAR name=ID
;

MultiLineMatrixLiteral hidden(WHITESPACE, SL_COMMENT, ML_COMMENT) :
	entryType=MathematicalType? dot=Dotting?
	'{' NEWLINE
		(lines+=MatrixLiteralLine)+
	'}'
;

PlaceholderEntry :
	{PlaceholderEntry} '.'
;

MatrixEntry :
	Expression | PlaceholderEntry
;

MatrixLiteralLine :
	entries+=MatrixEntry (','? entries+=MatrixEntry)* NEWLINE
;

Dotting :
	'(' DOT fill=Expression ')'
;

// III. b) simple expressions
GenericVariable :
	name=GENERIC_VAR
;

Variable :
	ref=[VarDeclaration]
;

Function hidden() :
	ref=[FuncDeclaration|QualifiedFunctionName]
	genericArguments=GenericArguments
	'('
;

QualifiedFunctionName :
	(ID '::')? ID
;

FunctionCall :
	ref=Function (params+=Expression (',' params+=Expression)*)? ')'
;

TupleExpression :
	'(' entries+=Expression (',' entries+=Expression)+ ')'
;

DeterminantOrAbsoluteValue :
	'|' expr=Expression '|'
;

ParenthesizedExpression :
	'(' expr=Expression ')'
;

TypeComparisonExpression :
	sub=Type 'is' sup=Type
;

AtomExpression returns Expression :
	MathematicalLiteral
	| BooleanLiteral
	| MultiLineMatrixLiteral
	| MatrixInit
	| FunctionCall
	| Variable
	| GenericVariable
	| ParenthesizedExpression
	| DeterminantOrAbsoluteValue
	| TupleExpression
	| TypeComparisonExpression
;

// III. c) complex expressions
VariableAssignment :
	variable=Variable OP_ASSIGN value=Expression
;

MatrixEntryAssignment :
	matr=AtomExpression '[' line=Expression ',' column=Expression ']' OP_ASSIGN value=Expression
;

TupleAssignment :
	'(' variables+=(Variable|EmptyTupleAssignmentComponent) (',' variables+=(Variable|EmptyTupleAssignmentComponent))+ ')' OP_ASSIGN tuple=Expression
;

EmptyTupleAssignmentComponent :
	{EmptyTupleAssignmentComponent}
;

EntryAccess returns Expression :
	AtomExpression ({EntryAccess.matr=current} '[' line=Expression ',' column=Expression ']')*
;

Negation returns Expression :
	EntryAccess | ({Negation} ('!'|'¬') expr=EntryAccess)
;

Transposition returns Expression :
	Negation ({Transposition.expr=current} '^' TRANSPOS)?
;

Exponentiation returns Expression :
	Transposition ({Exponentiation.left=current} '^' right=Transposition)*
;

UnaryMinus returns Expression :
	Exponentiation | {UnaryMinus} ('-' expr=Exponentiation)
;

Division returns Expression :
	UnaryMinus ({Division.left=current} '/' right=UnaryMinus)*
;

Multiplication returns Expression :
	Division ({Multiplication.left=current} '*' right=Division)*
;

Addition returns Expression :
	Multiplication ({Addition.left=current} op=AdditionOp right=Multiplication)*
;

CastExpression returns Expression :
	Addition ({CastExpression.expr=current} 'as' type=Type)?
;

TypeCheckExpression returns Expression :
	CastExpression ({TypeCheckExpression.expr=current} 'is' type=Type)?
;

Equivalency returns Expression :
	TypeCheckExpression ({Equivalency.left=current} '~' right=TypeCheckExpression)*
;

Comparison returns Expression :
	Equivalency ({Comparison.left=current} op=ComparisonOp right=Equivalency)*
;

Equals returns Expression :
	Comparison ({Equals.left=current} op=EqualsOp right=Comparison)*
;

LogicalAndOr returns Expression:
	Equals ({LogicalAndOr.left=current} op=LogicalOp right=Equals)*
;

Ternary returns Expression :
	LogicalAndOr ({Ternary.cond=current} '?' ifTrue=Ternary OP_COLON ifFalse=Ternary)?
;

Expression returns Expression :
	VariableAssignment
	| Ternary
	| MatrixEntryAssignment
	| TupleAssignment
;

// III. d) operator enums
enum AdditionOp :
	PLUS='+' | MINUS='-'
;

enum ComparisonOp :
	LESS='<' | GREATER='>' | LESSOREQUAL='<=' | LESSOREQUAL='≤' | GREATEROREQUAL='>=' | GREATEROREQUAL='≥'
;

enum LogicalOp :
	AND='&&' | AND='∧' | AND='and' | OR='||' | OR='∨' | OR='or'
;

enum EqualsOp :
	EQUALS='=' | EQUALS='==' | NOTEQUALS='!=' | NOTEQUALS='≠'
;

ID :
	ID_PATTERN | IMAGINARY_UNIT | TRANSPOS | X
;

// IV. terminals
terminal OP_ASSIGN : ':=';
terminal OP_COLON  : ':';

// IV. a) keywords:
terminal IF             : 'if';
terminal ELSE           : 'else';
terminal INT            : 'int';
terminal BOOL           : 'bool' 'ean'?;
terminal RETURN         : 'return';
terminal FAIL           : 'fail';
terminal DEF            : 'def';
terminal PROC           : 'proc';
terminal DOT            : 'dot';
terminal CONST          : 'const';
terminal VAR            : 'var';
terminal TRANSPOS       : 'T';
terminal IMAGINARY_UNIT : 'i';
terminal X              : 'x';
terminal INTERNAL       : 'internal';

// IV. b) misc 
terminal NEWLINE      : '\n';
terminal WHITESPACE   : (' '|'\t')+;
terminal ID_PATTERN   : (LETTER|'_')(LETTER|'_'|DIGIT_WITHOUT_ZERO|'0')*;
terminal GENERIC_VAR  : '$' ID_PATTERN;
terminal GENERIC_TYPE : '%' ID_PATTERN;
terminal PROC_NAME    : '@' ID_PATTERN;

terminal fragment DIGIT_WITHOUT_ZERO : '1'..'9';
terminal fragment LETTER : ('a'..'z'|'A'..'Z');

terminal STRING : "'" ("\\'"|!("'"))* "'";

// IV. c) numbers
terminal ZERO : '0';
terminal POSITIVE_INTEGER : DIGIT_WITHOUT_ZERO (DIGIT_WITHOUT_ZERO|ZERO)*;
terminal NEGATIVE_INTEGER : '-' POSITIVE_INTEGER;

terminal DOUBLE : '-'? (DIGIT_WITHOUT_ZERO | ZERO)+ '.' (DIGIT_WITHOUT_ZERO | ZERO)+;

// IV. d) comments
terminal SL_COMMENT : '§' !('§') !(NEWLINE)* NEWLINE?;
terminal ML_COMMENT : '§§' -> '§§';
